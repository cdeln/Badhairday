@vert {
#version 420

    in vec3 in_Position;
    in vec3 in_Normal;
    in vec2 in_TexCoord;

    out vec3 tc_Position;
    out vec3 tc_Normal;
    out vec2 tc_TexCoord;

    void main(void) {
        tc_Position = in_Position;
        tc_Normal = in_Normal;
        tc_TexCoord = in_TexCoord;
    }
}

@tesc {
#version 420

    layout(vertices = 1) out;
    in vec3 tc_Position[]; // From vertex shader
    in vec3 tc_Normal[]; // From vertex shader
    in vec2 tc_TexCoord[];

    uniform int tessLevel;

    struct BezierPatch {
        vec3 v300;
        vec3 v210;
        vec3 v120;
        vec3 v030;
        vec3 v021;
        vec3 v012;
        vec3 v003;
        vec3 v102;
        vec3 v201;
        vec3 v111;

        vec3 n300;
        vec3 n030;
        vec3 n003;

        vec2 t300;
        vec2 t030;
        vec2 t003;
    };

    out patch BezierPatch o; 

    vec3 project(vec3 point, vec3 origin, vec3 normal) {
        vec3 direction = point - origin; 
        float projlen = dot(direction, normal);
        vec3 residual = projlen * normal; 
        return point - residual; 
    }

    void main() {

        // Copy normals
        o.n300 = tc_Normal[0];
        o.n030 = tc_Normal[1];
        o.n003 = tc_Normal[2];

        // Copy tex coords
        o.t300 = tc_TexCoord[0];
        o.t030 = tc_TexCoord[0];
        o.t003 = tc_TexCoord[0];

        // Copy original vertices 
        o.v300 = tc_Position[0];
        o.v030 = tc_Position[1];
        o.v003 = tc_Position[2];

        // Calc edge vectors
        vec3 e300 = o.v003 - o.v030;
        vec3 e030 = o.v300 - o.v003;
        vec3 e003 = o.v030 - o.v300;

        // Calc midpoints on each edge
        o.v210 = o.v300 + e003 / 3;
        o.v120 = o.v300 + 2 * e003 / 3;

        o.v021 = o.v030 + e300 / 3;
        o.v012 = o.v030 + 2 * e300 / 3;

        o.v102 = o.v003 + e030 / 3;
        o.v201 = o.v003 + 2 * e030 / 3;

        // Project onto planes
        o.v210 = project(o.v210, o.v300, o.n300); 
        o.v120 = project(o.v120, o.v030, o.n030); 

        o.v021 = project(o.v021, o.v030, o.n030);
        o.v012 = project(o.v012, o.v003, o.n003);

        o.v102 = project(o.v102, o.v003, o.n003);
        o.v201 = project(o.v201, o.v300, o.n300);

        // Calc center point
        vec3 c = ( o.v300 + o.v030 + o.v003 ) / 3;
        o.v111 = (o.v210 + o.v120 + o.v021 + o.v012 + o.v102 + o.v201) / 6;
        o.v111 += (o.v111 - c) / 2;

        gl_TessLevelInner[0] = tessLevel; 
        gl_TessLevelOuter[0] = 1; 
        gl_TessLevelOuter[1] = 1; 
        gl_TessLevelOuter[2] = 1; 
    }
}

@tese{
#version 420

    layout(triangles) in;

    struct BezierPatch { 
        vec3 v300;
        vec3 v210;
        vec3 v120;
        vec3 v030;
        vec3 v021;
        vec3 v012;
        vec3 v003;
        vec3 v102;
        vec3 v201;
        vec3 v111;

        vec3 n300;
        vec3 n030;
        vec3 n003;

        vec2 t300;
        vec2 t030;
        vec2 t003;
    };

    in patch BezierPatch o;

    out vec3 g_Position;
    out vec3 g_Normal;
    out vec2 g_TexCoord;

    void main() {

        float u = gl_TessCoord.x;
        float v = gl_TessCoord.y;
        float w = gl_TessCoord.z; 

        float u2 = pow(u,2);
        float v2 = pow(v,2);
        float w2 = pow(w,2);

        float u3 = pow(u,3);
        float v3 = pow(v,3);
        float w3 = pow(w,3);

        g_Position =
            u3 * o.v300 + v3 * o.v030 + w3 * o.v003
            + 3 * u2 * v * o.v210 + 3 * u * v2 * o.v120
            + 3 * v2 * w * o.v021 + 3 * v * w2 * o.v012
            + 3 * w2 * u * o.v102 + 3 * w * u2 * o.v201
            + 6 * u * v * w * o.v111;

        g_Normal = u * o.n300 + v * o.n030 + w * o.n003;
        g_TexCoord = u * o.t300 + v * o.t030 + w * o.t003;
    }
}

@geom {
#version 150
    bool inFrustum( vec3 pos ) {
        bool x = -1 <= pos.x && pos.x <= 1;
        bool y = -1 <= pos.y && pos.y <= 1;
        bool z = -1 <= pos.z && pos.z <= 1;
        return x && y && z;
    }

    layout(triangles) in;
    layout(line_strip, max_vertices = 33) out;

    in vec3 g_Position[];
    in vec3 g_Normal[];
    in vec2 g_TexCoord[];

    out vec3 f_Normal;
    out vec3 f_View;
    out vec4 f_Color;
    out vec3 f_TexCoord;
    out vec3 f_hairColor;

    uniform mat4 modelViewMatrix;
    uniform mat4 projectionMatrix;
    uniform mat4 lightMatrix;
    uniform usampler3D sampler;
    uniform sampler2D tex;
    uniform vec3 forceCenter;
    uniform int numLayers;
    uniform float hairLength;
    uniform bool samplePerVertex;
    uniform float shadeFactor;

    void generateVertex(vec3 position, vec3 normal){
        mat3 normalMatrix = mat3(modelViewMatrix);
        mat4 M = projectionMatrix * modelViewMatrix;
        vec4 light_Position;
        float shade;

        light_Position = lightMatrix * vec4(position,1);
        light_Position /= light_Position.w;

        f_TexCoord = (vec3(light_Position) + 1.0) / 2.0;
        f_Color = texture(sampler, f_TexCoord);
        shade = pow(0.99, shadeFactor * f_Color.r);
        f_Color = vec4(vec3(shade), f_Color.a);
        if( ! inFrustum ( light_Position.xyz ) ) {
            f_Color = vec4(1,0,0,1);
        }

        f_Normal = normalMatrix * normal;
        f_View = vec3(modelViewMatrix * vec4(position,1));
        gl_Position = M * vec4(position, 1);
        EmitVertex();
    }

    void main() {

        vec4 material = texture(tex, g_TexCoord[2]);
        mat3 normalMatrix = mat3(modelViewMatrix);
        mat4 M = projectionMatrix * modelViewMatrix;
        int segments = 10;
        float segLength = 0.05;
        vec3 gravityForce = vec3(0,-1,0);
        f_hairColor = vec3(1);

        //Match colors with dot product. Alpha = 0 because it doesnt matter
        vec4 red = vec4(1,0,0,0);
        vec4 blue = vec4(0,0,1,0);
        vec4 green = vec4(0,1,0,0);
        vec4 white = vec4(1,1,1,0);
        vec4 black = vec4(0,0,0,0);

        if (dot(material, red) > 0.5) {
            segments = 5;
            segLength = 0.03; //Head
        }
        else if (dot(material, blue) > 0.5) {
            segLength = 0.06;
            gravityForce = vec3(0,-0.4,0); //Chest, mane
            f_hairColor =  vec3(0.8,0.8,0.2);
        }
        else if (dot(material, green) > 0.5 ) {
            segments = 5;
            segLength = 0.03; //Body
            f_hairColor =  vec3(0.6,0.6,0.4);
        }

        if(dot(material, white) > 2) {
            segLength = 0.04;
            gravityForce = vec3(0,-0.2,0); // Fluffy tail
        }
        else if (dot(material, material) < 0.2) {
            segments = 1;
            segLength = 0.0; // eyes no hair
            f_hairColor =  vec3(0,0,0);
        }


        vec3 midPos = vec3(0);
        vec3 midNormal =  vec3(0);
        for(int i = 0; i < 3; ++i) {
            midPos += g_Position[i];
            midNormal += g_Normal[i];
        }
        midPos /= 3;
        midNormal /= 3;

        vec3 hairRoot = midPos;
        vec3 segStartPos = hairRoot;
        vec3 currHairDirr = normalize(midNormal);
        vec3 force = vec3(0,0,0);
        int layer;
        for(int n = 0; n < segments; ++n)
        {
            generateVertex(segStartPos, currHairDirr);
            generateVertex(segStartPos + segLength * currHairDirr, currHairDirr);
            EndPrimitive();

            segStartPos += segLength * currHairDirr;

            force = normalize(segStartPos - forceCenter);
            float weight = exp( - distance(segStartPos,forceCenter));
            currHairDirr = normalize(currHairDirr + gravityForce + weight*force);

        }
    }

}

@frag {
#version 420

    in vec3 f_Normal;
    in vec3 f_View;
    in vec4 f_Color;
    in vec3 f_TexCoord;
    in vec3 f_hairColor;

    out vec4 out_Color;

    uniform vec3 light;
    uniform int numLayers;
    uniform float shadeFactor;
    uniform usampler3D sampler;
    uniform sampler2D depthSampler;
    uniform bool samplePerVertex;
    uniform bool enableShadowMap;
    uniform bool interpolate;
    
    float sampleShadow(vec3 depthCoord) {
        if ( texture( depthSampler, depthCoord.xy ).r < depthCoord.z ) {
            return 1; 
        }
        return 0;
    }

    float calcShadow(vec3 depthCoord) {

        vec2 texSize = textureSize(depthSampler, 0);
        float shadow = 0;
        vec3 offsetDepth = vec3(0);
        const int sampleRadius = 10;
        const int numSamples = (2*sampleRadius + 1) * (2*sampleRadius + 1);
        for(int x = -sampleRadius; x <= sampleRadius; ++x) {
            for(int y = -sampleRadius; y <= sampleRadius; ++y) {
                offsetDepth.xy = vec2(x,y) / texSize;
                shadow += sampleShadow(depthCoord + offsetDepth); 
            }
        }
        return shadow / numSamples;
    }

    float vcos(vec3 a, vec3 b) {
        return abs(dot(a,b));
    }
    
    float vsin(vec3 a, vec3 b) {
        return length(cross(a,b)); 
    }

    void main(void) {

        const float ambient = 0.1; 
        const float diffuseFactor = 1.0;
        const float specularFactor = 1.0;
        const float specularExp = 150;
        float diffuse, specular, visibility, shadeColor, shadeAlpha;

        vec3 l = normalize(light);
        vec3 n = normalize(f_Normal);
        vec3 v = normalize(- f_View);

        diffuse = length(cross(n, l)); // Hair shading
        diffuse = diffuseFactor * max(diffuse, 0);

        // Specular
        vec3 halfVector = normalize(l + v);
        //specular = max(sqrt(1 - pow(dot(half_Vector, n), 2)), 0); // Kajiya-Kay Model 
        //specular = dot(n,l) * dot(n, v) + vsin(n, l) * vsin(n, v); // As in the original paper? 
        specular = vsin(halfVector, n); // Ekvivalent med  
        specular = specularFactor * pow(specular, specularExp);

        if( samplePerVertex ) {
            shadeColor = ambient + f_Color.r * (diffuse + specular);
            shadeAlpha = 1;
        }
        else {

            //uvec4 texColor = texture(sampler, f_TexCoord);

            float shade = 0;
            ivec3 texSize = textureSize(sampler, 0);
            vec3 texPos = f_TexCoord * texSize;
            vec3 floorPos = floor( texPos ); 
            vec3 curPos;
            float fx,fy,fz;
            if( interpolate ) {
                for(int x = -1; x <= 1; ++x) {
                    for(int y = -1; y <= 1; ++y) {
                        for(int z = -1; z <= 1; ++z) {
                            curPos = floorPos + vec3(x,y,z) + vec3(0.5); // middle of pixel 
                            fx = max(0, 1 - abs(curPos.x - texPos.x));
                            fy = max(0, 1 - abs(curPos.y - texPos.y));
                            fz = max(0, 1 - abs(curPos.z - texPos.z));
                            shade += fx*fy*fz * texture(sampler, curPos / texSize).r;
                        }
                    }
                }
            }
            else {
                shade = texture(sampler, f_TexCoord).r;
            }

            visibility = pow(0.99, shadeFactor * shade / 10000);

            float shadow = 0;
            if( enableShadowMap ) {
                shadow = calcShadow(f_TexCoord);
            }

            float specularShadow = 1 - shadow;
            float diffuseShadow = 1 - shadow;
            shadeColor = ambient + visibility * ( diffuseShadow * diffuse + specularShadow * specular);
            shadeAlpha = 1;//tex_Color.a;
        }
        out_Color = vec4(vec3(shadeColor)*f_hairColor, shadeAlpha);
    }
}
